
def cloudsmithUser = project.hasProperty('cloudsmithUser') ? project.property('cloudsmithUser') : System.getenv('CLOUDSMITH_USER')
def cloudsmithKey = project.hasProperty('cloudsmithApiKey') ? project.property('cloudsmithApiKey') : System.getenv('CLOUDSMITH_API_KEY')

publishing {
    repositories {
        maven {
            name = "cloudsmith"
            url = "https://api-g.cloudsmith.io/maven/consensys/quorum-besu-plugins/"
            credentials {
                username = cloudsmithUser
                password = cloudsmithKey
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId "net.consensys.besu.plugin"
            version "${project.version}"
            from components.java

            versionMapping {
                usage('java-api') { fromResolutionOf('runtimeClasspath') }
                usage('java-runtime') { fromResolutionResult() }
            }
            suppressPomMetadataWarningsFor('testFixturesApiElements')
            suppressPomMetadataWarningsFor('testFixturesRuntimeElements')
            pom {
                name = "Besu plugins - ${project.name}"
                url = 'https://github.com/ConsenSys/besu-plugins'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/ConsenSys/besu-plugins.git'
                    developerConnection = 'https://github.com/ConsenSys/besu-plugins.git'
                    url = 'https://github.com/ConsenSys/besu-plugins'
                }
            }
        }
    }
}